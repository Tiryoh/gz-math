load("@rules_gz//gz:headers.bzl", "gz_include_header")
load("@rules_license//rules:license.bzl", "license")

package(
    default_applicable_licenses = [":license"],
)

license(
    name = "license",
    package_name = "gz-math-eigen3",
)

public_headers = glob([
    "include/gz/math/eigen3/*.hh",
])

gz_include_header(
    name = "eigen3_hh_genrule",
    out = "include/gz/math/eigen3.hh",
    hdrs = public_headers,
)

cc_library(
    name = "eigen3",
    srcs = public_headers,
    hdrs = public_headers + [
        "include/gz/math/eigen3.hh",
    ],
    includes = ["include"],
    deps = [
        "//:gz-math",
        "@eigen",
    ],
)

test_sources = glob(
    [
        "src/*_TEST.cc",
    ],
)

[cc_test(
    name = src.replace("/", "_").replace(".cc", "").replace("src_", ""),
    srcs = [src],
    deps = [
        ":eigen3",
        "@googletest//:gtest_main",
    ],
) for src in test_sources]
